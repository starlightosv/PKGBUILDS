# Maintainer: EndeavourOS <info@endeavouros.com>
pkgname=endeavouros-theming
pkgver=10.0
pkgrel=1
pkgdesc='Default EndeavourOS background images and icons for all different DE'
arch=('any')
replaces=('EndeavourOS-theming')
url='https://www.endeavouros.com'
license=('GPL3')
makedepends=('git')
source=("https://github.com/endeavouros-team/endeavouros-theming/archive/refs/tags/${pkgver}.tar.gz")
md5sums=('b582f62a8082e6e85d553951da3ad6ac')
sha512sums=('fe430d1ee730221d63bab3b3004fba846b94dd30b6107a8b293c8a4c49af00b5aa32f2e5ff78965d27f90dd79e01f2c9e903314a1cd08f44f8c8a5c7d030a914')
install=$pkgname.install

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -d "${pkgdir}/usr/share/endeavouros"
  install -d "${pkgdir}/usr/share/pixmaps"
  install -d "${pkgdir}/usr/share/endeavouros/backgrounds"

  install -Dm644 *.png *.svg           "${pkgdir}/usr/share/endeavouros"
  install -Dm644 endeavouros.{svg,png} "${pkgdir}/usr/share/pixmaps"
  install -Dm644 endeavouros-icon.png  "${pkgdir}/usr/share/pixmaps"
  
  cd backgrounds
  install -Dm644 *.png                 "${pkgdir}/usr/share/endeavouros/backgrounds"

  # make symlinks for KDE:
  cd "${pkgdir}/usr/share/pixmaps"
  ln -s endeavouros.png endeavouros-logo.png
  ln -s endeavouros.svg endeavouros-logo.svg

  # cleanup
  cd $srcdir
  rm -rf ../${pkgname}
}
